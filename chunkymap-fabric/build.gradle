plugins {
	id 'fabric-loom' version '1.7-SNAPSHOT'
	id "com.github.johnrengelman.shadow" version "8.1.1"
}

repositories {
	maven { url "https://repo.wertarbyte.com" }
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"chunkymap" {
			sourceSet sourceSets.main
		}
	}

}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	implementation "se.llbit:chunky-core:2.5.0-SNAPSHOT"
	
	shadow(implementation(project ":chunkymap-common"))
}

processResources {
	filesMatching("fabric.mod.json") {
		expand project.properties
	}
}

shadowJar {
	configurations = [project.configurations.shadow]
	exclude "META-INF"

	dependencies {
		// Explicitly exclude fastutil (otherwise included from chunky-core)
		// because it's already included in the Minecraft server jar.
		exclude(dependency("it.unimi.dsi:fastutil:"))
	}
}

remapJar {
	dependsOn shadowJar
	mustRunAfter shadowJar
	inputFile = file(shadowJar.archivePath)
}
