plugins {
	id "com.github.johnrengelman.shadow" version "8.1.1"
}

repositories {
	maven { url "https://oss.sonatype.org/content/repositories/releases" }
	maven { url "https://repo.wertarbyte.com" }
}

configurations {
	provided
}

sourceSets {
	main {
		compileClasspath += configurations.provided
	}
}

dependencies {
	shadow(implementation "com.squareup.okhttp3:okhttp:4.12.0")
	shadow(implementation "se.llbit:chunky-core:2.5.0-SNAPSHOT")
	shadow(implementation 'net.time4tea:oidnjni:0.1.13')
	shadow(implementation "de.lemaik:chunky-denoiser:0.5.0")

	provided files("../lib/Dynmap-3.7-beta-6-fabric-1.21.jar")
}

shadowJar {
	configurations = [project.configurations.shadow]
	exclude "META-INF"

	dependencies {
		// Explicitly exclude fastutil (otherwise included from chunky-core)
		// because it's already included in the Minecraft server jar.
		exclude(dependency("it.unimi.dsi:fastutil:"))
	}
}
